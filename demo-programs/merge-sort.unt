/*
 * This program implements a parallelized recursive merge sort.
 *
 * At each step, the array is split into two halves. Two threads work on the two halves
 * simultaneously. Each of those two threds itself spawns two threads, etc., so the level of
 * parallelism doubles for each level of recursion.
 *
 */
thread_def Sorter {
  // Receive our “instructions” in a tuple
  int start; int end; int[100] arr;
  receive {
    ((int s, int e), int[100] data) -> {
      start = s;
      end = e;
      arr = data;
    }
    _ -> {
      print("Should not be here :)");
      exit(1);
    }
  }

  // Base case
  if (start >= end) {
    parent << "Done";
  // Recursive case
  } else {
    // Spawn two child threads to work on each half of our workload
    int mid = (start + end) / 2;
    thread left = spawn Sorter;
    thread right = spawn Sorter;
    left << ((start, mid), arr);
    right << ((mid + 1, end), arr);
    // Wait for both to be done
    for (int i = 0; i < 2; i++) receive { _ -> {} }
    // “Merge” their results
    int[100] tmp;
    int counter = 0;
    (int i, int j) = (start, mid + 1);
    while ((i <= mid) && (j <= end)) {
      if (arr[i] <= arr[j]) {
        tmp[counter] = arr[i];
        i++;
      } else {
        tmp[counter] = arr[j];
        j++;
      }
      counter++;
    }
    while (i <= mid) {
      tmp[counter] = arr[i];
      i++;
      counter++;
    }
    while (j <= end) {
      tmp[counter] = arr[j];
      j++;
      counter++;
    }

    for (int z = 0; z < counter; z++) {
      arr[start + z] = tmp[z];
    }

    // Let our parent know we’ve finished our work here
    parent << "Done";
  }
}


thread_def Main {
  int SIZE = 100;
  int[100] arr;

  // Initialize array in reverse order
  for (int i = 0; i < SIZE; i++) {
    arr[i] = SIZE - i;
  }

  // Print unsorted array
  print("Before: ");
  for (int i = 0; i < SIZE; i++) {
    print(string_of_int(arr[i]) + " ");
  }
  print("\n");

  // Sort
  thread t = spawn Sorter;
  t << ((0, 100 - 1), arr);
  receive { _ -> {} }

  // Print final (sorted) array
  print("After: ");
  for (int i = 0; i < SIZE; i++) {
    print(string_of_int(arr[i]) + " ");
  }
  print("\n");
}
